apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'
apply plugin: 'kotlin-android'
apply from: "../bitmovinpropertiesloader.gradle"

def packageName = 'com.bitmovin.analytics'
def libraryVersion = '2.4.0'

android {
    namespace 'com.bitmovin.analytics.conviva'
    compileSdk rootProject.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField 'String', 'VERSION_NAME', "\"${libraryVersion}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        buildConfig true
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }
}

dependencies {
    api 'com.conviva.sdk:conviva-core-sdk:4.0.37'
    implementation bitmovinPlayerDependencies.bitmovinPlayer

    testImplementation testingDependencies.junit
    testImplementation testingDependencies.mockk
    testImplementation testingDependencies.strikt
    androidTestImplementation testingDependencies.androidx_junit
    androidTestImplementation testingDependencies.androidx_espresso_core
}

publishing {
    publications {
        release(MavenPublication) {
            groupId packageName
            version = libraryVersion
            artifactId project.getName()

            // Exclude kotlin-stdlib from the pom file as it is only needed for testing
            pom.withXml {
                Node pomNode = asNode()
                pomNode.dependencies.'*'.findAll() {
                    it.artifactId.text() == 'kotlin-stdlib'
                }.each() {
                    it.parent().remove(it)
                }
            }

            afterEvaluate {
                from components.release
            }
        }
    }
}

artifactory {
    contextUrl = 'https://bitmovin.jfrog.io/bitmovin'
    publish {
        repository {
            // The Artifactory repository key to publish to
            repoKey = libraryVersion.endsWith('SNAPSHOT') ? 'libs-snapshot-local' : 'libs-release-local'
            username = bitmovinProperties.getProperty('artifactory_user') // The publisher user name
            password = bitmovinProperties.getProperty('artifactory_password') // The publisher password
        }
        defaults {
            // Tell the Artifactory Plugin which artifacts should be published to Artifactory.
            publications('release')
            publishArtifacts = true

            // Properties to be attached to the published artifacts.
            properties = ['qa.level': 'basic', 'dev.team': 'core']
            // Publish generated POM files to Artifactory (true by default)
            publishPom = true
        }
    }
}
